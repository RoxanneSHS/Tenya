BEGIN ~B2#TENYJ~

IF ~~ THEN BEGIN 0
  SAY @0
  IF ~~ THEN EXTERN ~PUMBERL~ B2#Ten2
END

IF ~~ THEN BEGIN 0A
  SAY @1
  IF ~~ THEN DO ~SetGlobal("JALAPPEAR","GLOBAL",1)
CreateCreature("JALANT",[459.531],S)
ActionOverride("Jalantha",Dialog([PC]))
~ EXIT
END

IF ~~ THEN BEGIN 3
  SAY @2
  IF ~~ THEN DO ~ActionOverride("PUMBERL",Wait(10))
ActionOverride("PUMBERL",Enemy())
~ EXIT
END

IF ~~ THEN BEGIN 5
  SAY @3
  IF ~~ THEN EXTERN ~JALANT~ B2#Ten2
END

IF ~~ THEN BEGIN 6
  SAY @4
  IF ~~ THEN EXTERN ~JALANT~ B2#Ten3
END

IF ~~ THEN BEGIN 7
  SAY @5
  IF ~~ THEN EXTERN ~JALANT~ B2#Ten4
END

IF ~~ THEN BEGIN 9
  SAY @6
  IF ~~ THEN EXTERN ~TREMAI~ B2#Ten2
END

IF ~Global("B2#JalantDead","GLOBAL",1)~ THEN BEGIN 11
  SAY @7
  IF ~~ THEN DO ~SetGlobal("B2#JalantDead","GLOBAL",2)~ EXIT
END

IF ~Global("B2#TenDoll","GLOBAL",1)~ THEN BEGIN 12
  SAY @8
  IF ~~ THEN REPLY @9 GOTO 13
END

IF ~~ THEN BEGIN 13
  SAY @10
  IF ~~ THEN DO ~SetGlobal("B2#TenDoll","GLOBAL",2)~ EXIT
END

IF ~Global("B2#TenDrunk","GLOBAL",1)~ THEN BEGIN 14
  SAY @11
  = @12
  IF ~IsValidForPartyDialogue("Jaheira")~ THEN EXTERN ~JAHEIJ~ B2#TenD
  IF ~IsValidForPartyDialogue("Branwen")~ THEN EXTERN ~BRANWJ~ B2#TenD
  IF ~IsValidForPartyDialogue("Imoen")~ THEN EXTERN ~IMOEN2~ B2#TenD
  IF ~!IsValidForPartyDialogue("Jaheira")
!IsValidForPartyDialogue("Branwen")
!IsValidForPartyDialogue("Imoen")~ THEN REPLY @13 GOTO 15
END

IF ~~ THEN BEGIN 15
  SAY @14
   = @15
  IF ~~ THEN DO ~SetGlobal("B2#TenDrunk","GLOBAL",2)~ EXIT
END

IF ~Global("B2#TenyaCity","GLOBAL",1)~ THEN BEGIN 16
  SAY @16
   = @17
   = @18
  IF ~~ THEN REPLY @19 GOTO 17
  IF ~~ THEN REPLY @20 GOTO 18
  IF ~~ THEN REPLY @21 GOTO 19
END

IF ~~ THEN BEGIN 17
  SAY @22
   = @23
  IF ~~ THEN DO ~SetGlobal("B2#TenyaCity","GLOBAL",2)~ EXIT
END

IF ~~ THEN BEGIN 18
  SAY @24
  IF ~~ THEN DO ~SetGlobal("B2#TenyaCity","GLOBAL",2)~ EXIT
END

IF ~~ THEN BEGIN 19
  SAY @25
  IF ~~ THEN DO ~SetGlobal("B2#TenyaCity","GLOBAL",2)~ EXIT
END

IF ~~ THEN BEGIN 20
  SAY @26
  IF ~~ THEN REPLY @27 GOTO 21
END

IF ~~ THEN BEGIN 21
  SAY @28
  IF ~~ THEN REPLY @29 GOTO 22
  IF ~~ THEN REPLY @30 GOTO 22
END

IF ~~ THEN BEGIN 22
  SAY @31
  IF ~~ THEN DO ~SetGlobal("B2#TenChild","GLOBAL",1)~ EXIT
END

IF ~~ THEN BEGIN 23a
  SAY @32
  IF ~~ THEN EXTERN ~NOBLBA~ B2#TN2
END

IF ~~ THEN BEGIN 23b
  SAY @32
  IF ~~ THEN EXTERN ~NOBWBA~ B2#TN2
END

IF ~~ THEN BEGIN 24a
  SAY @33
  IF ~~ THEN EXTERN ~NOBLBA~ B2#TN3
END

IF ~~ THEN BEGIN 24b
  SAY @33
  IF ~~ THEN EXTERN ~NOBWBA~ B2#TN3
END

IF ~~ THEN BEGIN 25
  SAY @34
  IF ~~ THEN DO ~SetGlobal("B2#TenProst","GLOBAL",1)~ EXIT
END

IF ~Global("B2#TenTemp","GLOBAL",1)~ THEN BEGIN 26
  SAY @35
   = @36
  IF ~~ THEN REPLY @37 GOTO 27
  IF ~~ THEN REPLY @38 GOTO 29
END

IF ~~ THEN BEGIN 27
  SAY @39
  IF ~~ THEN REPLY @40 GOTO 28
  IF ~~ THEN REPLY @38 GOTO 29
END

IF ~~ THEN BEGIN 28
  SAY @41
  IF ~~ THEN GOTO 29
END

IF ~~ THEN BEGIN 29
  SAY @42
  IF ~~ THEN DO ~SetGlobal("B2#TenTemp","GLOBAL",2)~ EXIT
END

IF ~Global("B2#TenyaIsle","GLOBAL",1)~ THEN BEGIN 30
  SAY @43
  IF ~~ THEN REPLY @44 GOTO 31
END

IF ~~ THEN BEGIN 31
  SAY @45
   = @46
  IF ~~ THEN DO ~SetGlobal("B2#TenyaIsle","GLOBAL",2)~ EXIT
END   

IF ~Global("B2#TenyaMines","GLOBAL",1)~ THEN BEGIN 32
  SAY @90
   = @91
  IF ~~ THEN REPLY @92 GOTO 33
  IF ~~ THEN REPLY @93 GOTO 35
END

IF ~~ THEN BEGIN 33
  SAY @94
  IF ~~ THEN REPLY @95 GOTO 34
  IF ~~ THEN REPLY @96 GOTO 34
END

IF ~~ THEN BEGIN 34
  SAY @97
  IF ~~ THEN DO ~SetGlobal("B2#TenyaMines","GLOBAL",2)~ EXIT
END

IF ~~ THEN BEGIN 35
  SAY @98
  IF ~~ THEN DO ~SetGlobal("B2#TenyaMines","GLOBAL",2)~ EXIT
END

IF ~Global("B2#TenyaBandits","GLOBAL",1)~ THEN BEGIN 36
  SAY @99
  IF ~~ THEN REPLY @100 GOTO 37
  IF ~~ THEN REPLY @101 GOTO 39
END

IF ~~ THEN BEGIN 37
  SAY @102
   = @103
  IF ~~ THEN REPLY @104 GOTO 38
  IF ~~ THEN REPLY @105 GOTO 38
END

IF ~~ THEN BEGIN 38
  SAY @106
  IF ~~ THEN DO ~SetGlobal("B2#TenyaBandits","GLOBAL",2)~ EXIT
END

IF ~~ THEN BEGIN 39
  SAY @107
  IF ~~ THEN GOTO 37
END

IF ~Global("B2#TenyaCloakW","GLOBAL",1)~ THEN BEGIN 40
  SAY @108
  IF ~~ THEN REPLY @109 GOTO 41
  IF ~~ THEN REPLY @110 GOTO 42
END

IF ~~ THEN BEGIN 41
  SAY @111
  IF ~~ THEN GOTO 43
END

IF ~~ THEN BEGIN 42
  SAY @112
  IF ~~ THEN GOTO 43
END

IF ~~ THEN BEGIN 43
  SAY @113
  IF ~~ THEN REPLY @114 GOTO 44
  IF ~~ THEN REPLY @115 GOTO 46
END

IF ~~ THEN BEGIN 44
  SAY @116
  IF ~~ THEN REPLY @117 GOTO 45
END

IF ~~ THEN BEGIN 45
  SAY @118
   = @119
  IF ~~ THEN DO ~SetGlobal("B2#TenyaCloakW","GLOBAL",2)~ EXIT
END

IF ~~ THEN BEGIN 46
  SAY @120
   = @121
  IF ~~ THEN DO ~SetGlobal("B2#TenyaCloakW","GLOBAL",2)~ EXIT
END

IF ~Global("B2#TenyaCellar","GLOBAL",1)~ THEN BEGIN 47
  SAY @122
  IF ~~ THEN REPLY @123 GOTO 48
  IF ~~ THEN REPLY @124 GOTO 49
END

IF ~~ THEN BEGIN 48
  SAY @125000 //~I am not waiting anywhere! What is going on in there?~
  IF ~~ THEN REPLY @125 GOTO 49
  IF ~~ THEN REPLY @126 GOTO 50
  IF ~~ THEN REPLY @124 GOTO 49
END

IF ~~ THEN BEGIN 49
  SAY @127
  IF ~~ THEN REPLY @128 DO ~SetGlobal("B2#TenyaCellar","GLOBAL",2)~ EXIT
END

IF ~~ THEN BEGIN 50
  SAY @129
  IF ~~ THEN REPLY @130 GOTO 49
  IF ~~ THEN REPLY @131 GOTO 49
END

IF ~Global("B2#TenyaCandle","GLOBAL",1)~ THEN BEGIN 51
  SAY @132
  IF ~~ THEN REPLY @133 GOTO 52
  IF ~~ THEN REPLY @134 GOTO 54
END

IF ~~ THEN BEGIN 52
  SAY @135
  IF ~~ THEN REPLY @136 GOTO 53
  IF ~~ THEN REPLY @134 GOTO 54
END

IF ~~ THEN BEGIN 53
  SAY @137
  IF ~~ THEN DO ~SetGlobal("B2#TenyaCandle","GLOBAL",2)~ EXIT
END

IF ~~ THEN BEGIN 54
  SAY @138
  IF ~~ THEN DO ~SetGlobal("B2#TenyaCandle","GLOBAL",2)~ EXIT
END

IF ~Global("B2#TenyaCataComb","GLOBAL",1)~ THEN BEGIN 55
  SAY @139
  IF ~~ THEN REPLY @140 GOTO 56
  IF ~~ THEN REPLY @141 GOTO 57
END

IF ~~ THEN BEGIN 56
  SAY @142
  IF ~~ THEN REPLY @143 GOTO 57
END

IF ~~ THEN BEGIN 57
  SAY @144
  IF ~~ THEN DO ~SetGlobal("B2#TenyaCataComb","GLOBAL",2)~ EXIT
END

IF ~Global("B2#TenyaUndercity","GLOBAL",1)~ THEN BEGIN 58
  SAY @145
  IF ~~ THEN REPLY @146 GOTO 59
  IF ~~ THEN REPLY @147 GOTO 63
END

IF ~~ THEN BEGIN 59
  SAY @148
   = @149
  IF ~~ THEN REPLY @150 GOTO 60
  IF ~~ THEN REPLY @151 GOTO 60
END

IF ~~ THEN BEGIN 60
  SAY @152
   = @153
   = @154
  IF ~~ THEN REPLY @155 GOTO 61
  IF ~~ THEN REPLY @156 GOTO 63
END

IF ~~ THEN BEGIN 61
  SAY @157
  IF ~~ THEN REPLY @158 GOTO 62
END

IF ~~ THEN BEGIN 62
  SAY @159
   = @160
  IF ~~ THEN GOTO 64
END

IF ~~ THEN BEGIN 63
  SAY @161
  IF ~~ THEN GOTO 64
END

IF ~~ THEN BEGIN 64
  SAY @162
   = @163
  IF ~~ THEN DO ~SetGlobal("B2#TenyaUndercity","GLOBAL",2)~ EXIT
END

INTERJECT PUMBERL 1 1
  == B2#TENYJ   IF ~IsValidForPartyDialogue("Tenya")~ THEN
    @47
END PUMBERL B2#Ten1

INTERJECT PUMBERL 2 2
  == B2#TENYJ   IF ~IsValidForPartyDialogue("Tenya")~ THEN
    @48
END B2#TENYJ 3

INTERJECT JALANT 0 4
  == B2#TENYJ   IF ~IsValidForPartyDialogue("Tenya")~ THEN
    @49
END JALANT B2#Ten1

INTERJECT JALANT 13 8
  == B2#TENYJ   IF ~IsValidForPartyDialogue("Tenya")~ THEN
    @50
END JALANT B2#Ten5

INTERJECT VARCI 8 10
  == B2#TENYJ   IF ~IsValidForPartyDialogue("Tenya")~ THEN
    @51
END TREMAI 3

ALTER_TRANS VARCI
BEGIN 7 END
BEGIN 0 END
BEGIN
"TRIGGER"
~!IsValidForPartyDialogue("Tenya")~
END

EXTEND_BOTTOM VARCI 7
  IF ~IsValidForPartyDialogue("Tenya")
~ THEN EXTERN ~TREMAI~ B2#Ten1
END

APPEND TREMAI
IF ~~ THEN BEGIN B2#Ten1
  SAY @52
  IF ~~ THEN EXTERN ~B2#TENYJ~ 9
END

IF ~~ THEN BEGIN B2#Ten2
  SAY @53
   = @54
  IF ~~ THEN EXTERN ~VARCI~ 8
END
END

INTERJECT CHARLE 8 B2#TnAr1
  == B2#TENYJ   IF ~IsValidForPartyDialogue("Tenya")~ THEN
    @55
END CHARLE B2#TnAr2

APPEND CHARLE
IF ~~ THEN BEGIN B2#TnAr2
  SAY @56
  IF ~~ THEN EXTERN ~B2#TENYJ~ B2#TnAr3
END
END

APPEND B2#TENYJ
IF ~~ THEN BEGIN B2#TnAr3
  SAY @57
  IF ~~ THEN EXTERN ~CHARLE~ B2#TnAr4
END
END

APPEND CHARLE
IF ~~ THEN BEGIN B2#TnAr4
  SAY @58
  IF ~~ THEN EXTERN ~B2#TENYJ~ B2#TnAr5
END
END

APPEND B2#TENYJ
IF ~~ THEN BEGIN B2#TnAr5
  SAY @59
  IF ~~ THEN EXTERN ~CHARLE~ B2#TnAr6
END
END

APPEND CHARLE
IF ~~ THEN BEGIN B2#TnAr6
  SAY @60
  IF ~~ THEN REPLY @61 GOTO 15
  IF ~~ THEN REPLY @62 GOTO 14
  IF ~~ THEN REPLY @63 GOTO 9
END
END

INTERJECT RATCHI 3 B2#TnRC1
  == B2#TENYJ   IF ~IsValidForPartyDialogue("Tenya")~ THEN
    @64
END B2#TENYJ B2#TnRC2

APPEND B2#TENYJ
IF ~~ THEN BEGIN B2#TnRC2
  SAY @65
  IF ~~ THEN REPLY @66 GOTO B2#TnRC3
END

IF ~~ THEN BEGIN B2#TnRC3
  SAY @67
  IF ~~ THEN EXTERN ~RATCHI~ B2#TnRC4
END
END

APPEND RATCHI
IF ~~ THEN BEGIN B2#TnRC4
  SAY @68
  IF ~~ THEN DO ~EscapeArea()
~ EXIT
END
END

APPEND PUMBERL
IF ~~ THEN BEGIN B2#Ten1
  SAY @69
  IF ~~ THEN EXTERN ~B2#TENYJ~ 0
END

IF ~~ THEN BEGIN B2#Ten2
  SAY @70
  IF ~~ THEN REPLY @71 EXTERN ~B2#TENYJ~ 0A
END
END

APPEND JALANT
IF ~~ THEN BEGIN B2#Ten1
  SAY @72
  IF ~~ THEN EXTERN ~B2#TENYJ~ 5
END

IF ~~ THEN BEGIN B2#Ten2
  SAY @73
  IF ~~ THEN EXTERN ~B2#TENYJ~ 6
END

IF ~~ THEN BEGIN B2#Ten3
  SAY @74
  IF ~~ THEN EXTERN ~B2#TENYJ~ 7
END

IF ~~ THEN BEGIN B2#Ten4
  SAY @75
   = @76
  IF ~~ THEN REPLY #1385 GOTO 2
  IF ~~ THEN REPLY #1386 GOTO 3
  IF ~~ THEN REPLY #1387 GOTO 4
  IF ~~ THEN REPLY #16579 GOTO 13
END

IF ~~ THEN BEGIN B2#Ten5
  SAY @77
   = @78
   = @79
  IF ~~ THEN REPLY #16581 GOTO 14
  IF ~~ THEN REPLY #16582 GOTO 15
  IF ~~ THEN DO ~SetGlobal("B2#TenRan2","GLOBAL",1)~ REPLY #10696 EXIT
END
END

APPEND JAHEIJ
IF ~~ THEN BEGIN B2#TenD
  SAY @80
  IF ~~ THEN REPLY @81 EXTERN ~B2#TENYJ~ 15
  IF ~~ THEN REPLY @82 EXTERN ~B2#TENYJ~ 15
END
END

APPEND BRANWJ
IF ~~ THEN BEGIN B2#TenD
  SAY @80
  IF ~~ THEN REPLY @81 EXTERN ~B2#TENYJ~ 15
  IF ~~ THEN REPLY @82 EXTERN ~B2#TENYJ~ 15
END
END

APPEND IMOEN2
IF ~~ THEN BEGIN B2#TenD
  SAY @83
  IF ~~ THEN REPLY @81 EXTERN ~B2#TENYJ~ 15
  IF ~~ THEN REPLY @82 EXTERN ~B2#TENYJ~ 15
END
END

APPEND BOYBA1
IF WEIGHT #-1
~IsValidForPartyDialogue("Tenya")
Global("B2#TenChild","GLOBAL",0)~ THEN BEGIN B2#TC1
  SAY @84
  IF ~~ THEN EXTERN ~B2#TENYJ~ 20
END
END

APPEND GIRLBE
IF WEIGHT #-1
~IsValidForPartyDialogue("Tenya")
Global("B2#TenChild","GLOBAL",0)~ THEN BEGIN B2#TC1
  SAY @84
  IF ~~ THEN EXTERN ~B2#TENYJ~ 20
END
END

APPEND NOBLBA
IF WEIGHT #-1
~Name("Tenya",LastTalkedToBy)~ THEN BEGIN B2#TN1
  SAY @85
  IF ~~ THEN EXTERN ~B2#TENYJ~ 23a
END

IF ~~ THEN BEGIN B2#TN2
  SAY @86
  IF ~~ THEN EXTERN ~B2#TENYJ~ 24a
END

IF ~~ THEN BEGIN B2#TN3
  SAY @87
  IF ~~ THEN DO ~RunAwayFrom("Tenya",30)~ EXIT
END
END

APPEND NOBWBA
IF WEIGHT #-1
~Name("Tenya",LastTalkedToBy)~ THEN BEGIN B2#TN1
  SAY @85
  IF ~~ THEN EXTERN ~B2#TENYJ~ 23b
END

IF ~~ THEN BEGIN B2#TN2
  SAY @86
  IF ~~ THEN EXTERN ~B2#TENYJ~ 24b
END

IF ~~ THEN BEGIN B2#TN3
  SAY @87
  IF ~~ THEN DO ~RunAwayFrom("Tenya",30)~ EXIT
END
END

APPEND PROST1
IF WEIGHT #-1
~IsValidForPartyDialogue("Tenya")
Global("B2#TenProst","GLOBAL",0)~ THEN BEGIN B2#TP1
  SAY @88
  IF ~~ THEN EXTERN ~B2#TENYJ~ 25
END
END

APPEND PROST2
IF WEIGHT #-1
~Name("Tenya",LastTalkedToBy)~ THEN BEGIN B2#TP2
  SAY @89
  IF ~~ THEN EXIT
END
END

APPEND PROST3
IF WEIGHT #-1
~Name("Tenya",LastTalkedToBy)~ THEN BEGIN B2#TP2
  SAY @89
  IF ~~ THEN EXIT
END
END

APPEND PROST5
IF WEIGHT #-1
~Name("Tenya",LastTalkedToBy)~ THEN BEGIN B2#TP2
  SAY @89
  IF ~~ THEN EXIT
END
END

APPEND PROST6
IF WEIGHT #-1
~Name("Tenya",LastTalkedToBy)~ THEN BEGIN B2#TP2
  SAY @89
  IF ~~ THEN EXIT
END
END